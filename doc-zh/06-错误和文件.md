# 处理错误和多个文件

我们在上一章留下了一个未实现的函数，还有一些事情需要做才能真正称我们的程序为静态博客生成器。我们仍然需要处理目录中的多个文件，并创建一个带有指向其他页面链接的索引着陆页。

## HTML 中的链接

我们的 HTML EDSL 目前不支持链接或其他内容修饰符，如粗体和斜体。我们应该添加这些功能，以便在创建索引时使用它们。

到目前为止，我们一直将 `String` 传递给 `Structure`，创建了诸如 `p_` 和 `h_` 这样的函数。相反，我们可以创建并传递一个新的类型 `Content`，它可以是常规文本、链接、图像等。

---

**练习**：实现我们刚刚讨论的内容。按照编译器错误进行重构。

<details><summary>解决方案</summary>

<details><summary>src/Html/Internal.hs</summary>

```hs
module HsBlog.Html.Internal where

import Numeric.Natural

-- * Types

newtype Html
  = Html String

newtype Structure
  = Structure String

newtype Content
  = Content String

type Title
  = String

-- * EDSL

html_ :: Title -> Structure -> Html
html_ title content =
  Html
    ( el "html"
      ( el "head" (el "title" (escape title))
        <> el "body" (getStructureString content)
      )
    )

-- * Structure

p_ :: Content -> Structure
p_ = Structure . el "p" . getContentString

h_ :: Natural -> Content -> Structure
h_ n = Structure . el ("h" <> show n) . getContentString

ul_ :: [Structure] -> Structure
ul_ =
  Structure . el "ul" . concat . map (el "li" . getStructureString)

ol_ :: [Structure] -> Structure
ol_ =
  Structure . el "ol" . concat . map (el "li" . getStructureString)

code_ :: String -> Structure
code_ = Structure . el "pre" . escape

instance Semigroup Structure where
  (<>) c1 c2 =
    Structure (getStructureString c1 <> getStructureString c2)

instance Monoid Structure where
  mempty = Structure ""

-- * Content

txt_ :: String -> Content
txt_ = Content . escape

link_ :: FilePath -> Content -> Content
link_ path content =
  Content $
    elAttr
      "a"
      ("href=\"" <> escape path <> "\"")
      (getContentString content)

img_ :: FilePath -> Content
img_ path =
  Content $ "<img src=\"" <> escape path <> "\">"

b_ :: Content -> Content
b_ content =
  Content $ el "b" (getContentString content)

i_ :: Content -> Content
i_ content =
  Content $ el "i" (getContentString content)

instance Semigroup Content where
  (<>) c1 c2 =
    Content (getContentString c1 <> getContentString c2)

instance Monoid Content where
  mempty = Content ""

-- * Render

render :: Html -> String
render html =
  case html of
    Html str -> str

-- * Utilities

el :: String -> String -> String
el tag content =
  "<" <> tag <> ">" <> content <> "</" <> tag <> ">"

elAttr :: String -> String -> String -> String
elAttr tag attrs content =
  "<" <> tag <> " " <> attrs <> ">" <> content <> "</" <> tag <> ">"

getStructureString :: Structure -> String
getStructureString structure =
  case structure of
    Structure str -> str

getContentString :: Content -> String
getContentString content =
  case content of
    Content str -> str

escape :: String -> String
escape =
  let
    escapeChar c =
      case c of
        '<' -> "&lt;"
        '>' -> "&gt;"
        '&' -> "&amp;"
        '"' -> "&quot;"
        '\'' -> "&#39;"
        _ -> [c]
  in
    concat . map escapeChar


```

</details>

<details><summary>src/Html.hs</summary>

```hs
module HsBlog.Html
  ( Html
  , Title
  , Structure
  , html_
  , p_
  , h_
  , ul_
  , ol_
  , code_
  , Content
  , txt_
  , img_
  , link_
  , b_
  , i_
  , render
  )
  where

import HsBlog.Html.Internal
```

</details>

<details><summary>src/Convert.hs</summary>

```hs
module HsBlog.Convert where

import qualified HsBlog.Markup as Markup
import qualified HsBlog.Html as Html

convert :: Html.Title -> Markup.Document -> Html.Html
convert title = Html.html_ title . foldMap convertStructure

convertStructure :: Markup.Structure -> Html.Structure
convertStructure structure =
  case structure of
    Markup.Heading n txt ->
      Html.h_ n $ Html.txt_ txt

    Markup.Paragraph p ->
      Html.p_ $ Html.txt_ p

    Markup.UnorderedList list ->
      Html.ul_ $ map (Html.p_ . Html.txt_) list

    Markup.OrderedList list ->
      Html.ol_ $ map (Html.p_ . Html.txt_) list

    Markup.CodeBlock list ->
      Html.code_ (unlines list)
```

</details>

</details>

---

> 您可以查看
> [我们所做的更改](https://github.com/soupi/learn-haskell-blog-generator/commit/110a19029f0be42eb2ac656f5d38356dbf9c5746)
> 和[到目前为止的代码](https://github.com/soupi/learn-haskell-blog-generator/tree/110a19029f0be42eb2ac656f5d38356dbf9c5746)。

## 创建索引页面

使用我们扩展的 HTML EDSL，我们现在可以创建一个带有指向其他页面链接的索引页面。

要创建索引页面，我们需要一个文件列表及其*目标目的地*，以及它们的 `Markup`（这样我们就可以提取信息包含在我们的索引页面中，例如第一个标题和段落）。我们的输出应该是一个 `Html` 页面。

---

我们需要实现以下函数：

```hs
buildIndex :: [(FilePath, Markup.Document)] -> Html.Html
```

<details><summary>解决方案</summary>

```hs
buildIndex :: [(FilePath, Markup.Document)] -> Html.Html
buildIndex files =
  let
    previews =
      map
        ( \(file, doc) ->
          case doc of
            Markup.Heading 1 heading : article ->
              Html.h_ 3 (Html.link_ file (Html.txt_ heading))
                <> foldMap convertStructure (take 3 article)
                <> Html.p_ (Html.link_ file (Html.txt_ "..."))
            _ ->
              Html.h_ 3 (Html.link_ file (Html.txt_ file))
        )
        files
  in
    Html.html_
      "博客"
      ( Html.h_ 1 (Html.link_ "index.html" (Html.txt_ "博客"))
        <> Html.h_ 2 (Html.txt_ "文章")
        <> mconcat previews
      )
```


</details>

---

## 处理目录

我们处理整个目录的一般策略是：

- 创建输出目录
- 获取目录中的所有文件名
- 根据它们的扩展名进行过滤；我们想要处理 `txt` 文件并
  复制其他文件而不进行修改
- 我们想要解析每个文本文件，构建结果的索引，
  将文件转换为 HTML，并将所有内容写入目标目录

虽然我们的解析函数不能真正失败，但尝试从文件系统读取或写入文件
可能会以多种方式失败。如果我们的静态博客生成器足够健壮，以至于在单个文件
出现问题时不会完全失败，那将是非常好的。这是学习 Haskell 中错误处理的绝佳机会，
无论是在非效果代码中还是在 I/O 代码中。

在接下来的几章中，我们将探讨 Haskell 中错误处理的概况，
然后找出适合我们用例的正确方法。 